# Let's output to a jpeg file
set terminal jpeg size 1280,900

# This sets the aspect ratio of the graph
set size 1, 1

# The file we'll write to
if (!exists("outfilename")) outfilename='out.jpg'
set output outfilename

# The graph title
set title "Benchmark testing"

# Where to place the legend/key
set key left top

# Draw gridlines oriented on the y axis
set grid y

# Label the x-axis
set xlabel 'requests'

# Label the y-axis
set ylabel "response time (ms)"

# Tell gnuplot to use tabs as the delimiter instead of spaces (default)
set datafile separator '\t'

# Plot the data
if (!exists("filename")) filename='out.data'
plot filename every ::2 using 5 title 'response time' with lines


#######################################################
# Output to a jpeg file
set terminal jpeg size 1400,720

# Set the aspect ratio of the graph
set size 1, 1

# The file to write to
if (!exists("toutfilename")) toutfilename='out-timeseries.jpg'
set output toutfilename

# The graph title
set title "Benchmark testing"

# Where to place the legend/key
set key left top

# Draw gridlines oriented on the y axis
set grid y

# Specify that the x-series data is time data
set xdata time

# Specify the *input* format of the time data
set timefmt "%s"

# Specify the *output* format for the x-axis tick labels
set format x "%S"

# Label the x-axis
set xlabel 'seconds'

# Label the y-axis
set ylabel "response time (ms)"

# Tell gnuplot to use tabs as the delimiter instead of spaces (default)
set datafile separator '\t'

# Plot the data
plot filename every ::2 using 2:5 title 'response time' with points

